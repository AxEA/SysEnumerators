//
// mazzy, https://github.com/mazzy-ax/SysEnumerators
//
public class SysEnumerator_DataSourceMarked extends xSysEnumerator
{
	FormDataSource base;
	Common record;

    public FormDataSource base()
    {
        return base;
    }

    protected boolean begin()
    {
        boolean ret = super();
    
        if( ret )
        {
            record = this.base().getFirst(true);
            if( !record )
            {
                record = this.base().cursor();
            }
        }
    
        return ret;
    }

    public Common current()
    {
        return record;
    }

    public Common currentIdx()
    {
        return this.current();
    }

    public Common currentKey()
    {
        return this.currentValue();
    }

    public Common currentValue()
    {
        return this.current();
    }

    public int64 estimatedIterations(int64 estimatedIterations = -1)
    {
        int64 ret = 1;
    
        if( base.anyMarked() )
        {
            ret = super();
        }
    
        return ret;
    }

    protected boolean more()
    {
        return (record.RecId != 0);
    }

    public void new(FormDataSource _base)
    {;
        Debug::assert(_base != null);
    
        base = _base;
        super();
    }

    protected boolean next()
    {
        boolean ret = super();
    
        if( ret )
        {
            record = base.getNext();
        }
    
        return ret;
    }

    public common record()
    {
        return record;
    }

    public static SysEnumerator_DataSourceMarked construct(FormDataSource base)
    {
        SysEnumerator_DataSourceMarked enumerator = new SysEnumerator_DataSourceMarked(base);
    
        return enumerator;
    }
}