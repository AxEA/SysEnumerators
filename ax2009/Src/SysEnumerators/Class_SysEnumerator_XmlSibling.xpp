//
// mazzy, https://github.com/mazzy-ax/SysEnumerators
//
public class SysEnumerator_XmlSibling extends xSysEnumerator
{
	XmlNode node;
	int idx;

    public XmlNode base()
    {
        return node;
    }

    public XmlNode current()
    {
        return node;
    }

    public int currentIdx()
    {
        return idx;
    }

    public int currentKey()
    {
        return this.currentIdx();
    }

    public XmlNode currentValue()
    {
        return this.current();
    }

    protected void init()
    {
        idx = 0;
        super();
    }

    protected boolean more()
    {
        boolean ret = super() && this.base();
    
        return ret;
    }

    public void new(XmlNode _node = null)
    {
        node = _node;
        super();
    }

    protected boolean next()
    {
        boolean ret = super();
    
        if( ret )
        {
            if( this.base() )
            {
                node = this.base().nextSibling();
                idx++;
            }
            else
            {
                node = null;
            }
        }
    
        return ret;
    }

    public void reset()
    {
        throw error(Error::wrongUseOfFunction(funcname()));
    }

    public static SysEnumerator_XmlSibling construct(XmlNode node = null)
    {
        SysEnumerator_XmlSibling enumerator = new SysEnumerator_XmlSibling(node);
    
        return enumerator;
    }
}