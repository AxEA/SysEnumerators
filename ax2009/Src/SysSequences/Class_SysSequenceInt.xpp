//
// mazzy, https://github.com/mazzy-ax/SysEnumerators
//
public class SysSequenceInt extends SysSequence implements ISysSequence
{
    SysRangeInt range;
    int step;

    public SysSequenceInt clone()
    {
        SysSequenceInt sequence = SysSequenceInt::construct(this.range().start(), this.range().end(), this.step());

        return sequence;
    }

    protected SysRangeInt defaultRange()
    {
        return SysRangeInt::construct();
    }

    public int end()
    {
        return this.range().end();
    }

    public boolean equal(SysSequenceInt sequence)
    {
        boolean ret = sequence
                      && (this.range().equal(sequence.range()))
                      && (this.step() == sequence.step());
        return ret;
    }

    public SysIterations estimatedIterations()
    {
        SysIterations ret = SysEnumerator::IterationsMax();
        int distance = this.range().distance();
        boolean overflow = this.range().cover(0) && (distance <= 0);

        if( !overflow )
        {
            if( distance == 0 )
            {
                ret = 1;
            }
            else if( ((distance < 0) && (this.step() > 0))
                  || ((distance > 0) && (this.step() < 0)) )
            {
                ret = 0;
            }
            else if( step == 1 )
            {
                ret = distance + 1;
            }
            else
            {
                ret = distance div this.step() + 1;
            }
        }

        return ret;
    }

    public SysEnumerator getEnumerator()
    {
        return SysEnumerator::newFromSequenceInt(this);
    }

    public int getNext(int value)
    {
        return value + this.step();
    }

    public void new(SysRangeInt _range, int _step = 1)
    {;
        Debug::assert(_step != 0);

        if ( !_range )
        {
            _range = this.defaultRange();
        }

        range = _range;
        step = _step;
    }

    public SysSequenceInt normalize()
    {
        SysSequenceInt sequence;

        if( this.range().reversed() )
        {
            sequence = this.reverse();
        }
        else
        {
            sequence = this.clone();
        }

        return sequence;
    }

    public SysRangeInt range()
    {
        return range;
    }

    public SysSequenceInt reverse()
    {
        SysSequenceInt sequence = SysSequenceInt::construct(this.range().end(), this.range().start(), this.reverseStep());

        return sequence;
    }

    public int reverseStep()
    {
        return 0-step;
    }

    public int start()
    {
        return this.range().start();
    }

    public int step()
    {
        return step;
    }

    public str toString()
    {
        return strfmt('%1:%2', this.step(), this.range().toString());
    }

    public static SysSequenceInt construct(int start = 1, int end = maxint(), int step = 1)
    {
        SysRangeInt range = SysRangeInt::construct(start, end);
        SysSequenceInt sequence = new SysSequenceInt(range, step);

        return sequence;
    }

    public static SysSequenceInt newFromRange(SysRangeInt range, int step = 1)
    {
        SysSequenceInt sequence = new SysSequenceInt(range, step);

        return sequence;
    }
}