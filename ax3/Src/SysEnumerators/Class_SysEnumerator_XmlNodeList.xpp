//
// mazzy, https://github.com/mazzy-ax/SysEnumerators
//
public class SysEnumerator_XmlNodeList extends xSysEnumerator
{
    XmlNodeList base;
    XmlNode node;
    int idx;

    public XmlNodeList base()
    {
        return base;
    }

    protected boolean begin()
    {
        boolean ret = super() && this.next();

        return ret;
    }

    public anytype current()
    {
        return node;
    }

    public anytype currentIdx()
    {
        return idx;
    }

    public anytype currentKey()
    {
        return this.currentIdx();
    }

    public anytype currentValue()
    {
        return this.current();
    }

    public int estimatedIterations(int estimatedIterations = -1)
    {
        int ret = this.base().length();

        return ret;
    }

    protected void init()
    {
        idx = -1;
        super();
    }

    protected boolean more()
    {
        boolean ret = super() && (node != null);

        return ret;
    }

    public void new(XmlNodeList _base = null)
    {
        if( _base == null )
        {
            _base = XmlDocument::newBlank().root().selectNodes('');
        }

        base = _base;
        super();
    }

    protected boolean next()
    {
        boolean ret = super();

        if( ret )
        {
            if( this.base() )
            {
                node = this.base().nextNode();
                idx++;
            }
            else
            {
                node = null;
            }
        }

        return ret;
    }

    public void reset()
    {
        if( this.base() )
        {
            this.base().reset();
        }
        super();
    }

    public static SysEnumerator_XmlNodeList construct(XmlNodeList base)
    {
        SysEnumerator_XmlNodeList enumerator = new SysEnumerator_XmlNodeList(base);

        return enumerator;
    }
}