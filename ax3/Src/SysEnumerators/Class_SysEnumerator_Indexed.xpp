//
// mazzy, https://github.com/mazzy-ax/SysEnumerators
//
public abstract class SysEnumerator_Indexed extends xSysEnumerator
{
    SysSequence sequence;
    int currentIdx;

    public anytype currentIdx()
    {
        return currentIdx;
    }

    protected boolean currentIdxProvided()
    {
        return true;
    }

    protected int defaultEnd()
    {
        return 0;
    }

    protected int defaultStart()
    {
        return 1;
    }

    protected int defaultStep()
    {
        return 1;
    }

    public int estimatedIterations(int estimatedIterations = 0)
    {
        int ret = this.sequence().estimatedIterations();

        return ret;
    }

    protected void init()
    {
        currentIdx = this.sequence().start();
    }

    protected boolean more()
    {
        boolean ret = super();
        int idx;

        if( ret )
        {
            idx = this.currentIdx();
            ret = this.sequence().contains(idx);
        }

        return ret;
    }

    public void new(SysSequence _sequence = null)
    {;
        sequence = this.sequenceOrDefault(_sequence);
        super();
    }

    protected boolean next()
    {
        boolean ret = super();
        int idx;

        if( ret )
        {
            idx = this.currentIdx();
            currentIdx = this.sequence().getNext(idx);
        }

        return ret;
    }

    public SysSequence sequence()
    {
        return sequence;
    }

    protected SysSequence sequenceOrDefault(SysSequence _sequence = null)
    {
        if( _sequence == null )
        {
            _sequence = SysSequence::construct(this.defaultStart(), this.defaultEnd(), this.defaultStep());
        }

        return _sequence;
    }
}