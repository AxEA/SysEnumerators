//
// mazzy, https://github.com/mazzy-ax/SysEnumerators
//
public class SysSequenceDate extends SysSequence
{
    int step;

    public SysSequence clone()
    {
        SysSequenceDate sequence = SysSequenceDate::construct(this.start(), this.end(), this.step());

        return sequence;
    }

    public int64 estimatedIterations()
    {
        int64 ret = super();
        int distance = this.distance();

        if( distance == 0 )
        {
            ret = 1;
        }
        else if( ((distance < 0) && (this.step() > 0))
              || ((distance > 0) && (this.step() < 0)) )
        {
            ret = 0;
        }
        else if( step == 1 )
        {
            ret = distance + 1;
        }
        else
        {
            ret = distance div this.step() + 1;
        }

        return ret;
    }

    public anytype getNext(anytype value)
    {
        Date val = value;
        Date ret = val + this.step();

        return ret;
    }

    public void new(date _start = DateNull(), date _end = DateMax(), int _step = 1)
    {;
        Debug::assert(_step != 0);

        step = _step;
        range = SysRangeDate::construct(_start, _end);
    }

    public SysSequence reverse()
    {
        SysSequenceDate sequence = SysSequenceDate::construct(this.end(), this.start(), this.reverseStep());

        return sequence;
    }

    public anytype reverseStep()
    {
        return -step;
    }

    public anytype step()
    {
        return step;
    }

    public static SysSequenceDate construct(date _start = DateNull(), date _end = DateMax(), int _step = 1)
    {
        SysSequenceDate sequence = new SysSequenceDate(_start, _end, _step);

        return sequence;
    }
}