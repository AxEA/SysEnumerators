//
// mazzy, https://github.com/mazzy-ax/SysEnumerators
//
public class SysSequenceReal extends SysSequence
{
    real step;

    public SysSequence clone()
    {
        SysSequenceReal sequence = SysSequenceReal::construct(this.start(), this.end(), this.step());

        return sequence;
    }

    public int64 estimatedIterations()
    {
        int64 ret = int64Max();
        Amount distance = this.distance();
        boolean overflow = this.cover(0) && (distance <= 0.00001);

        if( overflow )
        {
            ret = int64Max();
        }
        else if( distance == 0 )
        {
            ret = 1;
        }
        else if( ((distance < 0) && (this.step() > 0))
              || ((distance > 0) && (this.step() < 0)) )
        {
            ret = 0;
        }
        else if( step == 1 )
        {
            ret = any2int64(distance + 1.0);
        }
        else
        {
            ret = any2int64(distance / this.step() + 1.0);
        }

        return ret;
    }

    public SysEnumerator getEnumerator()
    {
        return SysEnumerator::newFromToReal(this.start(), this.end(), this.step());
    }

    public anytype getNext(anytype value)
    {
        real val = value;
        real ret = val + this.step();

        return ret;
    }

    public void new(real _start = 1.0, real _end = RealMax(), real _step = 1.0)
    {;
        Debug::assert(_step != 0.0);

        step = _step;
        range = SysRangeReal::construct(_start, _end);
    }

    public SysSequence reverse()
    {
        SysSequenceReal sequence = SysSequenceReal::construct(this.end(), this.start(), this.reverseStep());

        return sequence;
    }

    public anytype reverseStep()
    {
        return -step;
    }

    public anytype step()
    {
        return step;
    }

    public static SysSequenceReal construct(real _start = 1, real _end = RealMax(), real _step = 1)
    {
        SysSequenceReal sequence = new SysSequenceReal(_start, _end, _step);

        return sequence;
    }
}