//
// mazzy, https://github.com/mazzy-ax/SysEnumerators
//
public class SysSequenceInt extends SysSequence
{
    int step;

    public SysSequence clone()
    {
        SysSequenceInt sequence = SysSequenceInt::construct(this.start(), this.end(), this.step());

        return sequence;
    }

    public int64 estimatedIterations()
    {
        int64 ret = super();
        int distance = this.distance();
        boolean overflow = this.cover(0) && (distance <= 0);

        if( overflow )
        {
            ret = int64Max();
        }
        else if( distance == 0 )
        {
            ret = 1;
        }
        else if( ((distance < 0) && (this.step() > 0))
              || ((distance > 0) && (this.step() < 0)) )
        {
            ret = 0;
        }
        else if( step == 1 )
        {
            ret = distance + 1;
        }
        else
        {
            ret = distance div this.step() + 1;
        }

        return ret;
    }

    public SysEnumerator getEnumerator()
    {
        return SysEnumerator::newFromSequenceInt(this);
    }

    public anytype getNext(anytype value)
    {
        int val = value;
        int ret = val + this.step();

        return ret;
    }

    public void new(int _start = 1, int _end = maxint(), int _step = 1)
    {;
        Debug::assert(_step != 0);

        step = _step;
        range = SysRangeInt::construct(_start, _end);
    }

    public SysSequence reverse()
    {
        SysSequenceInt sequence = SysSequenceInt::construct(this.end(), this.start(), this.reverseStep());

        return sequence;
    }

    public anytype reverseStep()
    {
        return -step;
    }

    public anytype step()
    {
        return step;
    }

    public static SysSequenceInt construct(int _start = 1, int _end = maxint(), int _step = 1)
    {
        SysSequenceInt sequence = new SysSequenceInt(_start, _end, _step);

        return sequence;
    }
}