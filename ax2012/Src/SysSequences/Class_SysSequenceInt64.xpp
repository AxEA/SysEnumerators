//
// mazzy, https://github.com/mazzy-ax/SysEnumerators
//
public class SysSequenceInt64 extends SysSequence
{
    int64 step;

    public SysSequence clone()
    {
        SysSequenceInt64 sequence = SysSequenceInt64::construct(this.start(), this.end(), this.step());

        return sequence;
    }

    public int64 estimatedIterations()
    {
        int64 ret = super();
        int64 distance = this.distance();
        boolean overflow = this.cover(0) && (distance <= 0);

        if( overflow )
        {
            ret = int64Max();
        }
        else if( distance == 0 )
        {
            ret = 1;
        }
        else if( ((distance < 0) && (this.step() > 0))
              || ((distance > 0) && (this.step() < 0)) )
        {
            ret = 0;
        }
        else if( step == 1 )
        {
            ret = distance + 1;
        }
        else
        {
            ret = distance div this.step() + 1;
        }

        return ret;
    }

    public SysEnumerator getEnumerator()
    {
        return SysEnumerator::newFromSequenceInt64(this);
    }

    public anytype getNext(anytype value)
    {
        int64 val = value;
        int64 ret = val + this.step();

        return ret;
    }

    public void new(int64 _start = 1, int64 _end = int64Max(), int64 _step = 1)
    {;
        Debug::assert(_step != 0);

        step = _step;
        range = SysRangeInt64::construct(_start, _end);
    }

    public SysSequence reverse()
    {
        SysSequenceInt64 sequence = SysSequenceInt64::construct(this.end(), this.start(), this.reverseStep());

        return sequence;
    }

    public anytype reverseStep()
    {
        return -step;
    }

    public anytype step()
    {
        return step;
    }

    public static SysSequenceInt64 construct(int64 _start = 1, int64 _end = int64Max(), int64 _step = 1)
    {
        SysSequenceInt64 sequence = new SysSequenceInt64(_start, _end, _step);

        return sequence;
    }
}