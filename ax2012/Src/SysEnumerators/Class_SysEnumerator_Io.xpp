//
// mazzy, https://github.com/mazzy-ax/SysEnumerators
//

/// <summary>
/// Базовый энумератор для CommaIo, AsciiIo, TextIo, BynaryIo, CommaTxtIo
/// </summary>
public class SysEnumerator_Io extends xSysEnumerator
{
    Io base;
    container current;

    public anytype base()
    {
        return base;
    }

    protected boolean begin()
    {
        boolean ret = super() && this.next();

        return ret;
    }

    public anytype current()
    {
        return current;
    }

    public anytype currentIdx()
    {
        return this.current();
    }

    public anytype currentKey()
    {
        return this.current();
    }

    public anytype currentValue()
    {
        return this.current();
    }

    protected boolean currentValueProvided()
    {
        return true;
    }

    protected boolean more()
    {
        boolean ret = this.base() && (this.base().status() == IO_Status::Ok);

        return ret;
    }

    public void new(Io _base = null)
    {
        base = _base;
        super();
    }

    protected boolean next()
    {
        boolean ret = super();

        if( ret && this.base() )
        {
            current = this.base().read();
        }

        return ret;
    }

    public void reset()
    {
        throw error(Error::wrongUseOfFunction(funcname()));
    }

    public static SysEnumerator_Io construct(Io base = null)
    {
        SysEnumerator_Io enumerator = new SysEnumerator_Io(base);

        return enumerator;
    }

}